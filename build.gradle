description = " Gmind7 Bakery DevOps Service Projects"

defaultTasks 'build'

ext {
	gradleScriptDir = "${rootProject.projectDir}/gradle"
}

task wrapper(type: Wrapper) {
	description = "Generates gradlew[.bat] scripts"
	gradleVersion = "$gradleVersion"
	
	doLast() {
	  def gradleOpts = "-XX:MaxPermSize=1024m -Xmx1024m"
	  def gradleBatOpts = "$gradleOpts -XX:MaxHeapSize=256m"
	  File wrapperFile = file("gradlew")
	  wrapperFile.text = wrapperFile.text.replace("DEFAULT_JVM_OPTS=",
												  "GRADLE_OPTS=\"$gradleOpts \$GRADLE_OPTS\"\nDEFAULT_JVM_OPTS=")
	  File wrapperBatFile = file("gradlew.bat")
	  wrapperBatFile.text = wrapperBatFile.text.replace("set DEFAULT_JVM_OPTS=",
														"set GRADLE_OPTS=$gradleBatOpts %GRADLE_OPTS%\nset DEFAULT_JVM_OPTS=")
	}
}

configure(allprojects) {
	
	apply plugin: "java"
	apply plugin: "eclipse"
	apply plugin: "eclipse-wtp"	
	
	apply from: "${gradleScriptDir}/maven.gradle"
	apply from: "${gradleScriptDir}/idea.gradle"
	
	project.sourceCompatibility = 1.7
	project.targetCompatibility = 1.7
	
	[compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"
  
	configurations.all {
		exclude group: "commons-logging"
		exclude module: "slf4j-log4j12"
		exclude module: "hibernate-jpa-2.0-api"
		resolutionStrategy.eachDependency { DependencyResolveDetails details ->
			if ((details.requested.group == 'org.springframework') && (details.requested.name != 'spring-test-mvc') ) {
			  details.useVersion springVersion
			}
			if (details.requested.group == 'org.springframework.security') {
			  details.useVersion springSecurityVersion
			}
			if (details.requested.group == 'org.slf4j') {
			  details.useVersion slf4jVersion
			}
		}
		resolutionStrategy.failOnVersionConflict()
		// don't cache changing modules at all
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
		// cache dynamic versions for 10 minutes
		resolutionStrategy.cacheDynamicVersionsFor 10*60, 'seconds'
		resolutionStrategy.forcedModules = ["org.jboss.logging:jboss-logging:$jbossLoggingVersion",
											"xml-apis:xml-apis:$xmlApisVersion",
											"org.hamcrest:hamcrest-core:$hamcrestVersion",
											"org.javassist:javassist:$javassistVersion",
											"org.objenesis:objenesis:$objenesisVersion",
											"org.aspectj:aspectjrt:$aspectjVersion",
											"org.aspectj:aspectjweaver:$aspectjVersion",
											"ch.qos.logback:logback-classic:$logbackVersion",
											"org.springframework.data:spring-data-commons:$springDataCommonsVersion",
											"org.springframework.hateoas:spring-hateoas:$springHateoasVersion",
											"org.springframework.plugin:spring-plugin-core:$springPluginVersion",
											"org.hibernate:hibernate-entitymanager:$hibernateVersion",
											"net.sf.ehcache:ehcache-core:$ehcacheVersion",
											"mysql:mysql-connector-java:$mysqlVersion",
											"com.google.guava:guava:$guavaVersion",
											"junit:junit:$junitVersion"]
	}
	
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "http://repo.spring.io/plugins-release" }
		maven { url "http://repo.spring.io/plugins-milestone" }
		maven { url "http://repo.spring.io/plugins-snapshot" }
		maven { url "https://raw.github.com/gmind7/gmind7.maven.repository/master/lib-releases" }
	}
	
	dependencies {
	  // Logging
	  compile "ch.qos.logback:logback-classic:$logbackVersion"
	  compile "org.slf4j:slf4j-api:$slf4jVersion"
	  runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
	  compile "org.codehaus.janino:janino:$janinoVersion"
	  
	  // Testing
	  testCompile "junit:junit:$junitVersion"
	  testCompile "org.hamcrest:hamcrest-library:$hamcrestVersion"
	  testCompile "com.jayway.jsonpath:json-path:$jsonpathVersion"
	  testCompile "org.mockito:mockito-core:$mockitoVersion"
	}
	
}

configure(subprojects) { subproject ->
	
	group = "${group}"
	version = "${version}"
	
	subprojectname = subproject.name;
	subprojectVersion = version;
	
	//apply from: "${gradleScriptDir}/archives.gradle"
	apply from: "${gradleScriptDir}/test.gradle"
	
	task initSrc << {
		project.sourceSets*.allSource.srcDirTrees.flatten().dir.each { dir ->
			dir.mkdirs()
		}
	}
	
	/* Eclipse Project 기본 폴더 생성 설정 */
	tasks.eclipse.dependsOn cleanEclipse
	tasks.eclipse.dependsOn initSrc
	
	/* Eclipse Wtp Deployment Assembly에서 Test제외 */
	eclipse.classpath.file.whenMerged { cp ->
		def testSrcs = cp.entries.findAll {
			it instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder &&
			 it.path.startsWith("src/test/")
		 }
	 
		 testSrcs.each {
			it.output = relativePath("./build/testClasses")
			it.exported = false // Deployment Assembly에서 제외
		 }
	}
	
	dependencies {
	  // Spring
	  compile("org.springframework:spring-core:$springVersion") { force = true }
	  compile("org.springframework:spring-beans:$springVersion") { force = true }
	  compile("org.springframework:spring-context:$springVersion") { force = true }
	  compile("org.springframework:spring-context-support:$springVersion") { force = true }
	  compile("org.springframework:spring-aop:$springVersion") { force = true }
	  compile("org.springframework:spring-aspects:$springVersion") { force = true }
	  compile("org.springframework:spring-expression:$springVersion") { force = true }
	  compile("org.springframework:spring-tx:$springVersion") { force = true }
	  //Spring Web
	  compile("org.springframework:spring-web:$springVersion") { force = true }

	  // AspectJ
	  compile "org.aspectj:aspectjrt:$aspectjVersion"
	  compile "org.aspectj:aspectjweaver:$aspectjVersion"
	  compile "aopalliance:aopalliance:$aopallianceVersion"
	  
	  // APIs
	  compile "javax.inject:javax.inject:$javaxInjectVersion"
	  
	  // Apache Commons
	  compile "org.apache.commons:commons-lang3:$apacheCommonsLang3Version"
	  compile "commons-io:commons-io:$apacheCommonsIOVersion"
	  compile "commons-fileupload:commons-fileupload:$apacheCommonsFileuploadVersion"
	  compile "commons-beanutils:commons-beanutils:$apacheCommonsBeanutilsVersion"
	  compile "commons-collections:commons-collections:$apacheCommonsCollectionsVersion"
	  compile "commons-configuration:commons-configuration:$apacheCommonsConfigurationVersion"
	  compile "commons-pool:commons-pool:$apacheCommonsPoolVersion"
	  compile "org.apache.httpcomponents:httpclient:$apacheHttpclientVersion"
	  
	  // JSR 303 with Hibernate Validator
	  compile "javax.validation:validation-api:$javaxValidationVersion"
	  compile "org.hibernate:hibernate-validator:$hibernateValidatorVersion"
	  
	  // Jackson
	  compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion"
	  compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate4:$jacksonVersion"
	  
	  // Gson
	  compile "com.google.code.gson:gson:$gsonVersion"
	  
	  // Ehcache
	  compile "org.hibernate:hibernate-ehcache:$hibernateVersion"
	  compile "net.sf.ehcache:ehcache-core:$ehcacheVersion"
	  
	  // Etc Supporting libraries
	  runtime "cglib:cglib-nodep:$cglibVersion"
	  compile "com.google.guava:guava:$guavaVersion"
	  compile "joda-time:joda-time:$jodaVersion"
	  compile "org.jadira.usertype:usertype.core:$jadiraVersion"
	  
	  // Testing
	  testCompile("org.springframework:spring-test:$springVersion") { force = true }	  	  
	}
}

project("devops-shared"){
	description = "[Module] Project Shared Files"
}

project("devops-integration-ldap"){
	
	description = "[Module] Lightweight Directory Access Protocol"
	
	dependencies {
		// Project		
		compile project(":devops-shared")
		
		compile "org.springframework.ldap:spring-ldap-core:$springldapVersion"
	}
	
}

project("devops-integration-sonar"){
	
	description = "[Module] SonarQube Using the Web Service Java client"
	
	dependencies {
		// Project
		compile project(":devops-shared")
		
		compile "org.codehaus.sonar:sonar-ws-client:$sonarVersion"		
		compile "org.springframework.ldap:spring-ldap-core:$springldapVersion"
	}
	
}

project("devops-data-jpa"){
	
	apply from: "${gradleScriptDir}/querydsl.gradle"
	
	description = "[Data] JPA Repository"
	
	dependencies {
		// Project
		compile project(":devops-shared")
		
		// Spring JPA
		compile("org.springframework:spring-orm:$springVersion") { force = true }
		compile("org.springframework:spring-jdbc:$springVersion") { force = true }
		compile("org.springframework.data:spring-data-jpa:$springDataJpaVersion") { force=true }
		compile("org.springframework.data:spring-data-commons:$springDataCommonsVersion") { force = true }
		
		// RDBMS
		runtime "org.hsqldb:hsqldb:$hsqldbVersion"
		compile "org.apache.tomcat:tomcat-jdbc:$apacheTomcatVersion"
		compile "mysql:mysql-connector-java:$mysqlVersion"
		
		// Hibernate
		compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
		
		// Mybatis
		compile "org.mybatis:mybatis:$mybatisVersion"
		compile "org.mybatis:mybatis-spring:$mybatisSpringVersion"
		
		// QueryDSL
		querydslapt "com.mysema.querydsl:querydsl-apt:$querydslVersion"
		compile "com.mysema.querydsl:querydsl-jpa:$querydslVersion"	
		
		// Log
		compile "org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4:$log4j2Version"
	}
	
}

project("devops-dashboard-web"){
	
	description = "[Web] Dashboard Web Service"
	
	apply plugin: "war"
	
	dependencies {
		// Project
		compile project(":devops-shared")
		compile project(":devops-integration-sonar")
		compile project(":devops-integration-ldap")
		compile project(":devops-data-jpa")		
	}
}

apply from: "${gradleScriptDir}/application.gradle"
apply from: "${gradleScriptDir}/sonar.gradle"
apply from: "${gradleScriptDir}/war.gradle"